         TITLE     'Meerkat Stats Exit'
*
DMGSMF   START     ,
*
*        *************************************************************
*        *                                                           *
*        * Description.                                              *
*        * ------------                                              *
*        *                                                           *
*        * Program is modification for Meerkat Computer Services     *
*        * Limited of supplied DGAXSMF to facilitate writing all     *
*        * Connect Direct statistics records to SMF using supplied   *
*        * parameters to produce user record type and to enhance     *
*        * supplied routine.                                         *
*        *                                                           *
*        * Difference to supplied routine are unique record number   *
*        * can be supplied and exclusion table means records can     *
*        * be excluded from being written if required.               *
*        *                                                           *
*        * Date Written: Summer 2020.                                *
*        *                                                           *
*        * Programmer:   John Bradley.                               *
*        *                                                           *
*        * Attributes:   RENT, REUS, RELOC, REFR.                    *
*        *                                                           *
*        * Register Information.                                     *
*        * -------- ------------                                     *
*        *                                                           *
*        * Register 1 points to a 4 byte address for the following   *
*        * parameter: Connect:Direct Statistics Record.              *
*        *                                                           *
*        *       R0     =     WORK REGISTER.                         *
*        *       R1     =     RDW POINTER (SMF RECORD)               *
*        *       R2     =     HOLD REGISTER FOR STATS REC ADDRESS    *
*        *       R3     =     DSECT BASE. FROM GETMAIN               *
*        *       R4     =     GETMAIN WORK AREA                      *
*        *       R5     =     WORK REGISTER.                         *
*        *       R6     =     WORK REGISTER.                         *
*        *       R7     =     WORK REGISTER.                         *
*        *       R8     =     WORK REGISTER.                         *
*        *       R9     =     WORK REGISTER.                         *
*        *       R10    =     WORK REGISTER.                         *
*        *       R11    =     TCA                                    *
*        *       R12    =     BASE REGISTER.                         *
*        *       R13    =     SAVEAREA POINTER.                      *
*        *       R14    =     RETURN REGISTER.                       *
*        *       R15    =     ENTRY POINT.                           *
*        *                                                           *
*        *************************************************************
*
*        *************************************************************
*        *                                                           *
*        * DGA$SAMP copies relevant macros into code.                *
*        *                                                           *
*        *************************************************************
*
         DGA$SAMP  ,
*
*        *************************************************************
*        *                                                           *
*        * Copy DMGBLS from SYS1.CDI.SDGAMAC.                        *
*        *                                                           *
*        *************************************************************
*
         COPY      DMGBLS
*
*        *************************************************************
*        *                                                           *
*        * DMOS macro from SYS1.CDI.SDGAMAC sets OPSYS and release   *
*        * level for assembly to identify version and so on.         *
*        *                                                           *
*        *************************************************************
*
         DMOS
*
&LASTPTF SETC      'R000000'
&UID     SETC      'MK00000'           /* Local mod id.             */
*
         DMCTR  DSECT=YES              /* Map STATS records.        */
         DMTCA  DSECT=YES              /* Map Task Control Area.    */
         DMRAT  DSECT=YES              /* Map Resource Table.       */
         DMFSQCB DSECT=YES             /* Map Security CB.          */
*
D0       EQU   0                       /* Disp of 0.                */
D12      EQU   12                      /* Disp of 12.               */
D16      EQU   16                      /* Disp of 16.               */
L2       EQU   2                       /* Length of 2.              */
L4       EQU   4                       /* Length of 4.              */
*
***********************************************************************
*                                                                     *
* Main Code.                                                          *
*                                                                     *
***********************************************************************
*
&DMCSECT SCENTER BASE=(R12),GETMAIN=SMFDSECT#,                         X
               AMODE=31,RMODE=24,LASTPTF=&LASTPTF,UID=&UID
*
&DMCSECT SCSTART   WKAREA=WKAREA,WKAREA2=WKAREA2
*
         USING     SMFDSECT,R13        /* Base DSECT.               */
         L         R2,0(,R1)           /* Get STAT record addr.     */
         USING     CTR,R2              /* Map STAT record.          */
         L         R11,4(,R1)          /* Get SQCB address.         */
         USING     SQCB,R11            /* Map SQCB.                 */
         L         R11,SQTCA           /* Map TCA.                  */
         DROP      R11                 /* Unmap SQCB.               */
         USING     TCA,R11             /* Map the TCA.              */
         TIME      BIN                 /* Issue time macro.         */
         ST        R0,SMFTIME          /* Store SMF header time.    */
         ST        R1,SMFDATE          /* Store SMF header date.    */
         L         R1,16               /* Address CVT.              */
         L         R1,196(,R1)         /* Get SMCA address.         */
         MVC       SYSID,16(R1)        /* Set system ID for SMF rec.*/
         MVC       SMFSYS(2),SMFRCD    /* Set SMF record type.      */
         LH        R1,0(,R2)           /* Get LNGTH of STAT record. */
         CH        R1,=Y(L'DMREC)      /* How big is it?            */
         BH        USE1006             /* Too big?                  */
         CH        R1,=H'1'            /* Negative or 0?            */
         BL        USE1006             /* Too small.                */
         B         MOVEREC             /* Perfect move it in.       */
*
USE1006  DS        0H
*
***********************************************************************
*                                                                     *
* If you ended up here the record is negative or too big to fit.      *
*                                                                     *
* We force a length of 1006.                                          *
*                                                                     *
***********************************************************************
*
         LH        R1,=Y(L'DMREC)      /* Set length to 1006.       */
*
MOVEREC  DS        0H
         LR        R15,R1              /* Replicate for MVCL.       */
         LR        R5,R1               /* Save length for later.    */
         LA        R0,0(,R2)           /* Point at record.          */
         LA        R14,DMREC           /* Point at target for move. */
         MVCL      R14,R0              /* Move the record.          */
         LR        R1,R5               /* Load original length.     */
         AH        R1,RDWLEN           /* Build new RDW.            */
         STH       R1,SMFRDWLL         /* Store RDW in SMF record.  */
         XC        SMFRDWZZ,SMFRDWZZ   /* Zeroise it now stored.    */
         LA        R1,SMFOUT           /* Point at SMF record.      */
         SMFWTM    (1)                 /* Write it out.             */
         LTR       R15,R15             /* Is it 0?                  */
         BNZ       ERRPROC             /* No write error message.   */
         LR        R3,R15              /* Get return code.          */
         B         GETOUT              /* Return to caller.         */
*
***********************************************************************
*                                                                     *
* Process R15 > 0 for SMFWTM macro.                                   *
*                                                                     *
*    REG 15 = 4  not written record will not fit in SMF data set      *
*             8  RDW length less than 18 bytes                        *
*            16  SMF data set "FULL"                                  *
*            20  SMF record suppressed.                               *
*                                                                     *
***********************************************************************
*
ERRPROC  DS        0H
         LR        R3,R15              /* Load bad return code.     */
         B         *+0(R3)             /* Branch to send message.   */
         B         RC4
         B         RC8
         B         RC12
         B         RC16
         B         RC20
*
RC4      DS        0H
         WTO       'SMKT001E: -- SMF Write Failure - RC4',             X
               ROUTCDE=(2,11)
         B         GETOUT
*
RC8      DS        0H
         WTO       'SMKT001E: -- SMF Write Failure - RC8',             X
               ROUTCDE=(2,11)
         B         GETOUT
*
RC12     DS        0H
         WTO       'SMKT001E: -- SMF Write Failure - RC12',            X
               ROUTCDE=(2,11)
         B         GETOUT
*
RC16     DS        0H
         WTO       'SMKT001E: -- SMF Write Failure - RC16',            X
               ROUTCDE=(2,11)
         B         GETOUT
*
RC20     DS        0H
         WTO       'SMKT001E: -- SMF Write Failure - RC20',            X
               ROUTCDE=(2,11)
*
***********************************************************************
*                                                                     *
* Return to caller.                                                   *
*                                                                     *
***********************************************************************
*
GETOUT   DS        0H
         SCRETURN  RC=(R3)             /* Freemain and RC in R3.    */
*
***********************************************************************
*                                                                     *
* Define constants.                                                   *
*                                                                     *
***********************************************************************
*
         DS        0F
RDWLEN   DC        Y(DMREC-SMFOUT)     /* SMF hdr length.           */
SMFRCD   DC        X'0284'             /* SMF system type is ZOS &  */
*                                      /* RECORD TYPE = what is     */
*                                      /* selected by user.         */
CDID     DC        C'CD '              /* Connect Identifier.       */
*
***********************************************************************
*                                                                     *
* Define DSECT for SMF statistics record.                             *
*                                                                     *
***********************************************************************
*
SMFDSECT DSECT
         DS        9D                  /* Save Area.                */
WKAREA   DS        D                   /* Display macro WAREA.      */
WKAREA2  DS        8D                  /* Display macro WAREA.      */
         DEBUG     LINKAGE=(R10,DEBUG,DEBUG2)
DEBUG    DS        D                   /* Display macro WAREA.      */
DEBUG2   DS        D                   /* Display macro WAREA.      */
TIMEUSED DS        D                   /* Used by TIME macro.       */
         DS        1H
*
* SMF record follows.
*
SMFOUT   EQU       *
SMFRDWLL DS        BL2'0'              /* RDW Length (RDWLL).       */
SMFRDWZZ DS        BL2'0'              /* RDW Flags  (RDWZZ).       */
SMFSYS   DS        BL1'0'              /* Systype.                  */
SMFTYPE  DS        BL1'0'              /* Record type.              */
SMFTIME  DS        XL4                 /* Time.                     */
SMFDATE  DS        PL4                 /* Date.                     */
SYSID    DS        CL4                 /* SYSID.                    */
DMREC    DS        CL2048              /* Stats record.             */
SMFDSECT# EQU      *-SMFDSECT          /* Length of DSECT.          */
*
         SCEND     ,
/*
//
